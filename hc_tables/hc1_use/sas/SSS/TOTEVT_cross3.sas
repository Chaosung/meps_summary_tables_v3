%JOBSTART;
*********************************************************************
 * Task Number :    AH4.FF004                                                                                                                        
 * Program Name:    \\\programs.ahrq.local\programs\MEPS\AHRQ4_CY2\F_DSO\FF004ETH\Prog\SAS\TOTEVT.sas                                                                                                
 * Description :    Sample SAS program to calculate one estimate (TOTEXP) by 14 groups for 1996-2014                                                                                                                                                                                                  
 * Input Data:	   \\derived.ahrq.local\Derived\MEPS\AHRQ4_CY2\F_DSO\FF004ETH\Data\PERSdata9614.sas7bdat  
                   \\derived.ahrq.local\Derived\MEPS\AHRQ4_CY2\F_DSO\FF004ETH\Data\EVNTDATA9614.sas7bdat     
 * Output Data :    no                                                      
 * Programmer  :    Zhengyi Fang/ Bidong Liu                                                                                                                                      
 * Date        :    6/7/2017
*********************************************************************;
OPTIONS LS=200 PS=60 OBS=max MPRINT;
libname task "\\derived.ahrq.local\Derived\MEPS\AHRQ4_CY2\F_DSO\FF004ETH\Data";
libname taskest "\\derived.ahrq.local\derived\MEPS\AHRQ4_CY2\F_DSO\FF004ETH\R_Estimates\V4";

TITLE1 "AH4.FF004";
PROC FORMAT;
VALUE AGEGRPS
1='UNDER 5'
2='5-17'
3='18-44'
4='45-64'
5='65+'
;
VALUE AGEGRPS_V2X
1='UNDER 18'
2='18-64'
3='65+'
;

VALUE MARRIED
.='. MISSING'
1='MARRIED'
2='WIDOWED'
3='DIVORCED'
4='SEPARATED'
5='NEVER MARRIED'
6='Inapplicable (age < 16)'
;

VALUE INSURANCE
1='ANY PRIVATE'
2='PUBLIC ONLY'
3='UNINSURED'
;

VALUE HEALTH
.=". MISSING" 
1='EXCELLENT' 
2='VERY GOOD' 
3='GOOD' 
4='FAIR' 
5='POOR'
;

VALUE MENTAL_HEALTH
.=". MISSING"
1='EXCELLENT'
2='VERY GOOD'
3='GOOD'
4='FAIR'
5='POOR'
;

VALUE EMPLOYED
.='. MISSING'
-1='Inapplicable (age < 16)'
1='EMPLOYED'
2='NOT EMPLOYED'
;

VALUE RACE
1='HISPANIC'
2='WHITE'
3='BLACK'
4='American Indian, Alaska Native,'
5='Asian, Hawaiian, or Pacific Isla'
9='WHITE AND OTHER'
;

VALUE EDUCATION
.='. MISSING'
1='LESS THAN HIGH SCHOOL'
2='HIGH SCHOOL'
3='SOME COLLEGE'
4='Inapplicable (age < 18)'
;

VALUE insurance_v2X
1='<65, Any private'
2='<65, Public only'
3='<65, UNINSURED'
4='65+, MEDICARE ONLY'
5='65+, Medicare and private'
6='65+, Medicare and other public'
7='65+, NO MEDICARE'
;

VALUE POVERTY
1='Negative or Poor'
2='Near-poor'
3='LOW INCOME'
4='MIDDLE INCOME'
5='HIGH INCOME'
;

 VALUE SEX
    1='Male'
    2='Female';
    
   VALUE REGION
    1='NORTHEAST'
    2='MIDWEST'
    3='SOUTH'
    4='WEST';
    
    VALUE EVNTYPX
    1="DVT"
    2="ERT"
    3="HHT"
    4="OBV"
    5="OMA"
    6="OPT"
    7="RX"
    8="IPT"
    9="HHA"
    10="HHN"
    11="OPY"
    12="OPZ"
    13="OBD"
    14="OBO"
    ;
    
              
run;


PROC SORT DATA=TASK.PERSDATA9614 OUT=PER;
BY YEAR DUPERSID;
RUN;

PROC SORT DATA=TASK.EVNTDATA9614 OUT=EVNT;
BY YEAR DUPERSID;
RUN;

DATA ALL;
MERGE EVNT(IN=A) PER(IN=B KEEP=YEAR DUPERSID AGEGRPS AGEGRPS_V2 REGION MARRIED RACE SEX INSURANCE INSURANCE_V2 HEALTH MENTAL_HEALTH EDUCATION EMPLOYED POVERTY VARPSU VARSTR PERWTF);
BY YEAR DUPERSID;
sub=2;
IF A then sub=1;
RUN;



DATA RESULT;
   LENGTH  LEVELS2 $32;
levels2="XXXXXXXXXXXXXXXXX";
IF _N_ LT 0;
RUN;



%MACRO YY;
%DO YEAR=1996 %TO 2014;


DATA FY;
  SET ALL(WHERE=(YEAR=&YEAR));
  TOTAL=1;
  IND=1;
RUN;



ODS LISTING CLOSE;
PROC SURVEYMEANS DATA=FY  STD SUMWGT MISSING;
VAR TOTAL;
CLUSTER VARPSU; 
STRATA VARSTR;
WEIGHT PERWTF;
DOMAIN 


  SUB*EVNTYP*IND SUB*EVNTYP*AGEGRPS SUB*EVNTYP*AGEGRPS_V2 SUB*EVNTYP*REGION SUB*EVNTYP*MARRIED SUB*EVNTYP*RACE SUB*EVNTYP*SEX SUB*EVNTYP*INSURANCE SUB*EVNTYP*INSURANCE_V2 SUB*EVNTYP*HEALTH SUB*EVNTYP*MENTAL_HEALTH SUB*EVNTYP*EDUCATION SUB*EVNTYP*EMPLOYED SUB*EVNTYP*POVERTY
  
  SUB*HH_PROV*IND SUB*HH_PROV*AGEGRPS SUB*HH_PROV*AGEGRPS_V2 SUB*HH_PROV*REGION SUB*HH_PROV*MARRIED SUB*HH_PROV*RACE SUB*HH_PROV*SEX SUB*HH_PROV*INSURANCE SUB*HH_PROV*INSURANCE_V2 SUB*HH_PROV*HEALTH SUB*HH_PROV*MENTAL_HEALTH SUB*HH_PROV*EDUCATION SUB*HH_PROV*EMPLOYED SUB*HH_PROV*POVERTY
 
 SUB*OP_DR*IND SUB*OP_DR*AGEGRPS SUB*OP_DR*AGEGRPS_V2 SUB*OP_DR*REGION SUB*OP_DR*MARRIED SUB*OP_DR*RACE SUB*OP_DR*SEX SUB*OP_DR*INSURANCE SUB*OP_DR*INSURANCE_V2 SUB*OP_DR*HEALTH SUB*OP_DR*MENTAL_HEALTH SUB*OP_DR*EDUCATION SUB*OP_DR*EMPLOYED SUB*OP_DR*POVERTY

 SUB*OB_DR*IND SUB*OB_DR*AGEGRPS SUB*OB_DR*AGEGRPS_V2 SUB*OB_DR*REGION SUB*OB_DR*MARRIED SUB*OB_DR*RACE SUB*OB_DR*SEX SUB*OB_DR*INSURANCE SUB*OB_DR*INSURANCE_V2 SUB*OB_DR*HEALTH SUB*OB_DR*MENTAL_HEALTH SUB*OB_DR*EDUCATION SUB*OB_DR*EMPLOYED SUB*OB_DR*POVERTY
  ;

ODS OUTPUT DOMAIN=OUT1;
RUN;
ODS LISTING;



DATA OUT2(WHERE=(SUB=1) DROP=HH_PROV OP_DR OB_DR EVNTYP);
SET OUT1;
YEAR=&YEAR;
IF HH_PROV=1 THEN do; EVNTYP="HHA";end;
  ELSE IF HH_PROV=2 THEN do; EVNTYP="HHN";end;
IF OP_DR=1 THEN do; EVNTYP='OPY';end;
  ELSE IF OP_DR=2 THEN do; EVNTYP='OPZ';end;
IF OB_DR=1 THEN do; EVNTYP='OBD';end;
  ELSE IF OB_DR=2 THEN do; EVNTYP='OBO';end;
  
  IF EVNTYP="DVIS" then EVNTYPx=1;
   ELSE IF EVNTYP="EROM" then EVNTYPx=2;
    ELSE IF EVNTYP="HVIS" then EVNTYPx=3;
     ELSE IF EVNTYP="MVIS" then EVNTYPx=4;
      ELSE IF EVNTYP="OMED" then EVNTYPx=5;
       ELSE IF EVNTYP="OPAT" then EVNTYPx=6;
        ELSE IF EVNTYP="PMED" then EVNTYPx=7;
         ELSE IF EVNTYP="STAZ" then EVNTYPx=8;
          ELSE IF EVNTYP="HHA" then EVNTYPx=9;
           ELSE IF EVNTYP="HHN" then EVNTYPx=10;
            ELSE IF EVNTYP="OPY" then EVNTYPx=11;
             ELSE IF EVNTYP="OPZ" then EVNTYPx=12;
              ELSE IF EVNTYP="OBD" then EVNTYPx=13;
              ELSE IF EVNTYP="OBO" then EVNTYPx=14;
RUN;

DATA RESULT;
SET RESULT OUT2;

             
RUN;

%END;
%MEND YY;
%YY 





DATA OUT3 (WHERE=(GRP1 NE " ") KEEP=LEVELS1 LEVELS2 GRP1 GRP2 STDDEV SUMWGT YEAR);
  SET RESULT;
  LENGTH GRP1 GRP2 $15  LEVELS1 LEVELS2 $32;
   ARRAY VAR(15) IND AGEGRPS AGEGRPS_V2 REGION MARRIED RACE SEX INSURANCE INSURANCE_V2 HEALTH MENTAL_HEALTH EDUCATION EMPLOYED POVERTY EVNTYPx;
    
     
      DO I=1 TO 15;
        DO J=1 TO (15-I);
         IF VAR(I) NE . AND VAR(I+J) NE . THEN DO; 
           L1=VAR(I);
           L2=VAR(I+J);
           GRP1=VNAME(VAR(I));
           GRP2=VNAME(VAR(I+J));
         END;
       END;
     END;
     
    IF GRP2='IND' THEN LEVELS2="TOTAL";
    ELSE IF GRP2='AGEGRPS' THEN LEVELS2=PUT(L2, AGEGRPS.);
    ELSE IF GRP2='AGEGRPS_V2' THEN LEVELS2=PUT(L2, AGEGRPS_V2X.);
    ELSE IF GRP2='REGION' THEN LEVELS2=PUT(L2, REGION.);
    ELSE IF GRP2='MARRIED' THEN LEVELS2=PUT(L2, MARRIED.);
    ELSE IF GRP2='RACE' THEN LEVELS2=PUT(L2, RACE.);
    ELSE IF GRP2='SEX' THEN LEVELS2=PUT(L2, SEX.);
    ELSE IF GRP2='INSURANCE' THEN LEVELS2=PUT(L2, INSURANCE.);
    ELSE IF GRP2='INSURANCE_V2' THEN LEVELS2=PUT(L2, INSURANCE_V2X.);
    ELSE IF GRP2='HEALTH' THEN LEVELS2=PUT(L2, HEALTH.);
    ELSE IF GRP2='MENTAL_HEALTH' THEN LEVELS2=PUT(L2, MENTAL_HEALTH.);
    ELSE IF GRP2='EDUCATION' THEN LEVELS2=PUT(L2, EDUCATION.);
    ELSE IF GRP2='EMPLOYED' THEN LEVELS2=PUT(L2, EMPLOYED.);
    ELSE IF GRP2='POVERTY' THEN LEVELS2=PUT(L2, POVERTY.);
      ELSE IF GRP2='EVNTYPX' THEN LEVELS2=PUT(L2, EVNTYPX.);
    
    IF GRP1='IND' THEN LEVELS1="TOTAL";
    ELSE IF GRP1='AGEGRPS' THEN LEVELS1=PUT(L1, AGEGRPS.);
    ELSE IF GRP1='AGEGRPS_V2' THEN LEVELS1=PUT(L1, AGEGRPS_V2X.);
    ELSE IF GRP1='REGION' THEN LEVELS1=PUT(L1, REGION.);
    ELSE IF GRP1='MARRIED' THEN LEVELS1=PUT(L1, MARRIED.);
    ELSE IF GRP1='RACE' THEN LEVELS1=PUT(L1, RACE.);
    ELSE IF GRP1='SEX' THEN LEVELS1=PUT(L1, SEX.);
    ELSE IF GRP1='INSURANCE' THEN LEVELS1=PUT(L1, INSURANCE.);
    ELSE IF GRP1='INSURANCE_V2' THEN LEVELS1=PUT(L1, INSURANCE_V2X.);
    ELSE IF GRP1='HEALTH' THEN LEVELS1=PUT(L1, HEALTH.);
    ELSE IF GRP1='MENTAL_HEALTH' THEN LEVELS1=PUT(L1, MENTAL_HEALTH.);
    ELSE IF GRP1='EDUCATION' THEN LEVELS1=PUT(L1, EDUCATION.);
    ELSE IF GRP1='EMPLOYED' THEN LEVELS1=PUT(L1, EMPLOYED.);
    ELSE IF GRP1='POVERTY' THEN LEVELS1=PUT(L1, POVERTY.);
      ELSE IF GRP1='EVNTYPX' THEN LEVELS1=PUT(L1, EVNTYPX.);
    
    
    IF GRP1="AGEGRPS_V2" THEN GRP1="AGEGRPS";
    IF GRP2="AGEGRPS_V2" THEN GRP2="AGEGRPS";
    IF GRP1="INSURANCE_V2" THEN GRP1="INSURANCE";
    IF GRP2="INSURANCE_V2" THEN GRP2="INSURANCE";
    IF GRP1='EVNTYPX' THEN GRP1="EVENT";
    IF GRP2='EVNTYPX' THEN GRP2="EVENT";
    
    
    
    
RUN;
 


 
PROC SORT DATA=TASKest.EVT9614 OUT=REST(KEEP=GRP1 LEVELS1 GRP2 LEVELS2 TOTEVT TOTEVT_SE YEAR);
BY YEAR GRP1 LEVELS1 GRP2 LEVELS2;
RUN;

DATA REST2;
SET REST;
 LENGTH GRP3 GRP4 $15  LEVELS3 LEVELS4 $32;
GRP3=LOWCASE(COMPRESS(GRP1));
LEVELS3=LOWCASE(COMPRESS(LEVELS1));
GRP4=LOWCASE(COMPRESS(GRP2));
LEVELS4=LOWCASE(COMPRESS(LEVELS2));
RUN;

DATA REST3(DROP=GRP3 GRP4 LEVELS3 LEVELS4);
SET REST2;
GRP5=GRP3;
LEVELS5=LEVELS3;
GRP6=GRP4;
LEVELS6=LEVELS4;
RUN;


DATA REST4(DROP=GRP5 GRP6 LEVELS5 LEVELS6);
SET REST3;
GRP4=GRP5;
LEVELS4=LEVELS5;
GRP3=GRP6;
LEVELS3=LEVELS6;
RUN;

DATA REST_ALL;
SET REST2 REST4;
RUN;

PROC SORT DATA=REST_ALL nodupkey;
BY YEAR GRP3 LEVELS3 GRP4 LEVELS4;
RUN;


DATA RESULT2;
SET OUT3;
 LENGTH GRP3 GRP4 $15  LEVELS3 LEVELS4 $32;
GRP3=LOWCASE(COMPRESS(GRP1));
LEVELS3=LOWCASE(COMPRESS(LEVELS1));
GRP4=LOWCASE(COMPRESS(GRP2));
LEVELS4=LOWCASE(COMPRESS(LEVELS2));
RUN;

PROC SORT DATA=RESULT2 nodupkey;
BY YEAR GRP3 LEVELS3 GRP4 LEVELS4;
RUN;

DATA ALL;
MERGE RESULT2(IN=A KEEP=YEAR GRP3 LEVELS3 GRP4 LEVELS4  SUMWGT STDDEV) REST_ALL(IN=B KEEP=YEAR GRP3 LEVELS3 GRP4 LEVELS4 TOTEVT TOTEVT_SE);
BY YEAR GRP3 LEVELS3 GRP4 LEVELS4;
IF A;
TOTEVTX=TOTEVT/1000000;
TOTEVT_SEX=TOTEVT_SE/1000000;
SUMWGTX=SUMWGT/1000000;
STDDEVX=STDDEV/1000000;
DIFF_SUM=abs(round(SUM(TOTEVTX,-SUMWGTX), 0.1));
DIFF_SE=abs(round(SUM(TOTEVT_SEX, -STDDEVX),0.1));
RUN;

TITLE2 "# OF EVENTS (in millions) BY CROSS SECTIONAL DEMO*EVNTYP, FOR YEAR ";
PROC PRINT DATA=ALL(WHERE=(DIFF_SUM NE 0 OR DIFF_SE NE 0)) LABEL;
VAR YEAR GRP3 LEVELS3 GRP4 LEVELS4 TOTEVTx SUMWGTx DIFF_SUM TOTEVT_SEx STDDEVx DIFF_SE;
LABEL TOTEVTx="# of event R"
 SUMWGTx="# of event SAS"
  DIFF_SUM="# of event Diff" 
  TOTEVT_SEx="SE R"
  STDDEVx="SE SAS" 
  DIFF_SE="SE Diff"
  ;
FORMAT TOTEVTx SUMWGTx DIFF_SUM TOTEVT_SEx STDDEVx DIFF_SE COMMA20.1;
RUN;


%jobend;
