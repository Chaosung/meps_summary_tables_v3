%JOBSTART;
*********************************************************************
 * Task Number :    AH4.FF004                                                                                                                        
 * Program Name:    \\\programs.ahrq.local\programs\MEPS\AHRQ4_CY2\F_DSO\FF004ETH\Prog\SAS\TOTEVT.sas                                                                                                
 * Description :    Sample SAS program to calculate one estimate (TOTEXP) by 14 groups for 1996-2014                                                                                                                                                                                                  
 * Input Data:	   \\derived.ahrq.local\Derived\MEPS\AHRQ4_CY2\F_DSO\FF004ETH\Data\PERSdata9614.sas7bdat  
                   \\derived.ahrq.local\Derived\MEPS\AHRQ4_CY2\F_DSO\FF004ETH\Data\EVNTDATA9614.sas7bdat     
 * Output Data :    no                                                      
 * Programmer  :    Zhengyi Fang/ Bidong Liu                                                                                                                                      
 * Date        :    6/7/2017
*********************************************************************;
OPTIONS LS=200 PS=60 OBS=max MPRINT;
libname task "\\derived.ahrq.local\Derived\MEPS\AHRQ4_CY2\F_DSO\FF004ETH\Data";
libname taskest "\\derived.ahrq.local\derived\MEPS\AHRQ4_CY2\F_DSO\FF004ETH\R_Estimates\V4";

TITLE1 "AH4.FF004";
PROC FORMAT;
VALUE AGEGRPS
1='UNDER 5'
2='5-17'
3='18-44'
4='45-64'
5='65+'
;
VALUE AGEGRPS_V2X
1='UNDER 18'
2='18-64'
3='65+'
;

VALUE MARRIED
.='. MISSING'
1='MARRIED'
2='WIDOWED'
3='DIVORCED'
4='SEPARATED'
5='NEVER MARRIED'
6='Inapplicable (age < 16)'
;

VALUE INSURANCE
1='ANY PRIVATE'
2='PUBLIC ONLY'
3='UNINSURED'
;

VALUE HEALTH
.=". MISSING" 
1='EXCELLENT' 
2='VERY GOOD' 
3='GOOD' 
4='FAIR' 
5='POOR'
;

VALUE MENTAL_HEALTH
.=". MISSING"
1='EXCELLENT'
2='VERY GOOD'
3='GOOD'
4='FAIR'
5='POOR'
;

VALUE EMPLOYED
.='. MISSING'
-1='Inapplicable (age < 16)'
1='EMPLOYED'
2='NOT EMPLOYED'
;

VALUE RACE
1='HISPANIC'
2='WHITE'
3='BLACK'
4='American Indian, Alaska Native,'
5='Asian, Hawaiian, or Pacific Isla'
9='WHITE AND OTHER'
;

VALUE EDUCATION
.='. MISSING'
1='LESS THAN HIGH SCHOOL'
2='HIGH SCHOOL'
3='SOME COLLEGE'
4='Inapplicable (age < 18)'
;

VALUE insurance_v2X
1='<65, Any private'
2='<65, Public only'
3='<65, UNINSURED'
4='65+, MEDICARE ONLY'
5='65+, Medicare and private'
6='65+, Medicare and other public'
7='65+, NO MEDICARE'
;

VALUE POVERTY
1='Negative or Poor'
2='Near-poor'
3='LOW INCOME'
4='MIDDLE INCOME'
5='HIGH INCOME'
;

 VALUE SEX
    1='Male'
    2='Female';
    
   VALUE REGION
    1='NORTHEAST'
    2='MIDWEST'
    3='SOUTH'
    4='WEST';
run;


PROC SORT DATA=TASK.PERSDATA9614 OUT=PER;
BY YEAR DUPERSID;
RUN;

PROC SORT DATA=TASK.EVNTDATA9614 OUT=EVNT;
BY YEAR DUPERSID;
RUN;

DATA ALL;
MERGE EVNT(IN=A) PER(IN=B KEEP=YEAR DUPERSID AGEGRPS AGEGRPS_V2 REGION MARRIED RACE SEX INSURANCE INSURANCE_V2 HEALTH MENTAL_HEALTH EDUCATION EMPLOYED POVERTY VARPSU VARSTR PERWTF);
BY YEAR DUPERSID;
sub=2;
IF A then sub=1;
RUN;


DATA RESULT;
   LENGTH  LEVELS2 $32;
levels2="XXXXXXXXXXXXXXXXX";
IF _N_ LT 0;
RUN;

%MACRO YY;
%DO YEAR=1996 %TO 2014;

DATA FY;
  SET ALL(WHERE=(YEAR=&YEAR));
  TOTAL=1;
  IND=1;
RUN;



ODS LISTING CLOSE;
PROC SURVEYMEANS DATA=FY  STD SUMWGT MISSING;
VAR TOTAL;
CLUSTER VARPSU; 
STRATA VARSTR;
WEIGHT PERWTF;
DOMAIN 


  SUB*IND*AGEGRPS SUB*IND*AGEGRPS_V2 SUB*IND*REGION SUB*IND*MARRIED SUB*IND*RACE SUB*IND*SEX SUB*IND*INSURANCE SUB*IND*INSURANCE_V2 SUB*IND*HEALTH SUB*IND*MENTAL_HEALTH SUB*IND*EDUCATION SUB*IND*EMPLOYED SUB*IND*POVERTY
  SUB*AGEGRPS*REGION SUB*AGEGRPS*MARRIED SUB*AGEGRPS*RACE SUB*AGEGRPS*SEX SUB*AGEGRPS*INSURANCE SUB*AGEGRPS*INSURANCE_V2 SUB*AGEGRPS*HEALTH SUB*AGEGRPS*MENTAL_HEALTH SUB*AGEGRPS*EDUCATION SUB*AGEGRPS*EMPLOYED SUB*AGEGRPS*POVERTY  
  SUB*AGEGRPS_V2*REGION SUB*AGEGRPS_V2*MARRIED SUB*AGEGRPS_V2*RACE SUB*AGEGRPS_V2*SEX SUB*AGEGRPS_V2*INSURANCE SUB*AGEGRPS_V2*INSURANCE_V2 SUB*AGEGRPS_V2*HEALTH SUB*AGEGRPS_V2*MENTAL_HEALTH SUB*AGEGRPS_V2*EDUCATION SUB*AGEGRPS_V2*EMPLOYED SUB*AGEGRPS_V2*POVERTY
  SUB*REGION*MARRIED SUB*REGION*RACE SUB*REGION*SEX SUB*REGION*INSURANCE SUB*REGION*INSURANCE_V2 SUB*REGION*HEALTH SUB*REGION*MENTAL_HEALTH SUB*REGION*EDUCATION SUB*REGION*EMPLOYED SUB*REGION*POVERTY
  SUB*MARRIED*RACE SUB*MARRIED*SEX SUB*MARRIED*INSURANCE SUB*MARRIED*INSURANCE_V2 SUB*MARRIED*HEALTH SUB*MARRIED*MENTAL_HEALTH SUB*MARRIED*EDUCATION SUB*MARRIED*EMPLOYED SUB*MARRIED*POVERTY 
  SUB*RACE*SEX SUB*RACE*INSURANCE SUB*RACE*INSURANCE_V2 SUB*RACE*HEALTH SUB*RACE*MENTAL_HEALTH SUB*RACE*EDUCATION SUB*RACE*EMPLOYED SUB*RACE*POVERTY
  SUB*SEX*INSURANCE SUB*SEX*INSURANCE_V2 SUB*SEX*HEALTH SUB*SEX*MENTAL_HEALTH SUB*SEX*EDUCATION SUB*SEX*EMPLOYED SUB*SEX*POVERTY 
  SUB*INSURANCE*HEALTH SUB*INSURANCE*MENTAL_HEALTH SUB*INSURANCE*EDUCATION SUB*INSURANCE*EMPLOYED SUB*INSURANCE*POVERTY
  SUB*INSURANCE_V2*HEALTH SUB*INSURANCE_V2*MENTAL_HEALTH SUB*INSURANCE_V2*EDUCATION SUB*INSURANCE_V2*EMPLOYED SUB*INSURANCE_V2*POVERTY
  SUB*HEALTH*MENTAL_HEALTH SUB*HEALTH*EDUCATION SUB*HEALTH*EMPLOYED SUB*HEALTH*POVERTY
  SUB*MENTAL_HEALTH*EDUCATION SUB*MENTAL_HEALTH*EMPLOYED SUB*MENTAL_HEALTH*POVERTY
  SUB*EDUCATION*EMPLOYED SUB*EDUCATION*POVERTY
  SUB*EMPLOYED*POVERTY;


ODS OUTPUT DOMAIN=OUT1;
RUN;
ODS LISTING;

DATA OUT2;
SET OUT1;
YEAR=&YEAR;
RUN;

DATA RESULT(WHERE=(SUB=1));
SET RESULT OUT2;
RUN;




%END;
%MEND YY;
%YY 


DATA OUT3 (WHERE=(GRP1 NE " ") KEEP=LEVELS1 LEVELS2 GRP1 GRP2 STDDEV SUMWGT YEAR);
  SET RESULT;
   LENGTH GRP1 GRP2 $15  LEVELS1 LEVELS2 $32;;
   ARRAY VAR(14) IND AGEGRPS AGEGRPS_V2 REGION MARRIED RACE SEX INSURANCE INSURANCE_V2 HEALTH MENTAL_HEALTH EDUCATION EMPLOYED POVERTY;
    
     
      DO I=1 TO 14;
        DO J=1 TO (14-I);
         IF VAR(I) NE . AND VAR(I+J) NE . THEN DO; 
           L1=VAR(I);
           L2=VAR(I+J);
           GRP1=VNAME(VAR(I));
           GRP2=VNAME(VAR(I+J));
         END;
       END;
     END;
     
    IF GRP2='IND' THEN LEVELS2="TOTAL";
    ELSE IF GRP2='AGEGRPS' THEN LEVELS2=PUT(L2, AGEGRPS.);
    ELSE IF GRP2='AGEGRPS_V2' THEN LEVELS2=PUT(L2, AGEGRPS_V2X.);
    ELSE IF GRP2='REGION' THEN LEVELS2=PUT(L2, REGION.);
    ELSE IF GRP2='MARRIED' THEN LEVELS2=PUT(L2, MARRIED.);
    ELSE IF GRP2='RACE' THEN LEVELS2=PUT(L2, RACE.);
    ELSE IF GRP2='SEX' THEN LEVELS2=PUT(L2, SEX.);
    ELSE IF GRP2='INSURANCE' THEN LEVELS2=PUT(L2, INSURANCE.);
    ELSE IF GRP2='INSURANCE_V2' THEN LEVELS2=PUT(L2, INSURANCE_V2X.);
    ELSE IF GRP2='HEALTH' THEN LEVELS2=PUT(L2, HEALTH.);
    ELSE IF GRP2='MENTAL_HEALTH' THEN LEVELS2=PUT(L2, MENTAL_HEALTH.);
    ELSE IF GRP2='EDUCATION' THEN LEVELS2=PUT(L2, EDUCATION.);
    ELSE IF GRP2='EMPLOYED' THEN LEVELS2=PUT(L2, EMPLOYED.);
    ELSE IF GRP2='POVERTY' THEN LEVELS2=PUT(L2, POVERTY.);
    
    IF GRP1='IND' THEN LEVELS1="TOTAL";
    ELSE IF GRP1='AGEGRPS' THEN LEVELS1=PUT(L1, AGEGRPS.);
    ELSE IF GRP1='AGEGRPS_V2' THEN LEVELS1=PUT(L1, AGEGRPS_V2X.);
    ELSE IF GRP1='REGION' THEN LEVELS1=PUT(L1, REGION.);
    ELSE IF GRP1='MARRIED' THEN LEVELS1=PUT(L1, MARRIED.);
    ELSE IF GRP1='RACE' THEN LEVELS1=PUT(L1, RACE.);
    ELSE IF GRP1='SEX' THEN LEVELS1=PUT(L1, SEX.);
    ELSE IF GRP1='INSURANCE' THEN LEVELS1=PUT(L1, INSURANCE.);
    ELSE IF GRP1='INSURANCE_V2' THEN LEVELS1=PUT(L1, INSURANCE_V2X.);
    ELSE IF GRP1='HEALTH' THEN LEVELS1=PUT(L1, HEALTH.);
    ELSE IF GRP1='MENTAL_HEALTH' THEN LEVELS1=PUT(L1, MENTAL_HEALTH.);
    ELSE IF GRP1='EDUCATION' THEN LEVELS1=PUT(L1, EDUCATION.);
    ELSE IF GRP1='EMPLOYED' THEN LEVELS1=PUT(L1, EMPLOYED.);
    ELSE IF GRP1='POVERTY' THEN LEVELS1=PUT(L1, POVERTY.);
    
    
    IF GRP1="AGEGRPS_V2" THEN GRP1="AGEGRPS";
    IF GRP2="AGEGRPS_V2" THEN GRP2="AGEGRPS";
    IF GRP1="INSURANCE_V2" THEN GRP1="INSURANCE";
    IF GRP2="INSURANCE_V2" THEN GRP2="INSURANCE";
    
RUN;
    
 

 
PROC SORT DATA=TASKest.EVT9614 OUT=REST(KEEP=GRP1 LEVELS1 GRP2 LEVELS2 TOTEVT TOTEVT_SE YEAR);
BY YEAR GRP1 LEVELS1 GRP2 LEVELS2;
RUN;

DATA REST2;
SET REST;
 LENGTH GRP3 GRP4 $15  LEVELS3 LEVELS4 $32;
GRP3=LOWCASE(COMPRESS(GRP1));
LEVELS3=LOWCASE(COMPRESS(LEVELS1));
GRP4=LOWCASE(COMPRESS(GRP2));
LEVELS4=LOWCASE(COMPRESS(LEVELS2));
RUN;

DATA REST3(DROP=GRP3 GRP4 LEVELS3 LEVELS4);
SET REST2;
GRP5=GRP3;
LEVELS5=LEVELS3;
GRP6=GRP4;
LEVELS6=LEVELS4;
RUN;


DATA REST4(DROP=GRP5 GRP6 LEVELS5 LEVELS6);
SET REST3;
GRP4=GRP5;
LEVELS4=LEVELS5;
GRP3=GRP6;
LEVELS3=LEVELS6;
RUN;

DATA REST_ALL;
SET REST2 REST4;
RUN;

PROC SORT DATA=REST_ALL nodupkey;
BY YEAR GRP3 LEVELS3 GRP4 LEVELS4;
RUN;


DATA RESULT2;
SET OUT3;
 LENGTH GRP3 GRP4 $15  LEVELS3 LEVELS4 $32;
GRP3=LOWCASE(COMPRESS(GRP1));
LEVELS3=LOWCASE(COMPRESS(LEVELS1));
GRP4=LOWCASE(COMPRESS(GRP2));
LEVELS4=LOWCASE(COMPRESS(LEVELS2));
RUN;

PROC SORT DATA=RESULT2 nodupkey;
BY YEAR GRP3 LEVELS3 GRP4 LEVELS4;
RUN;

DATA ALL;
MERGE RESULT2(IN=A KEEP=YEAR GRP3 LEVELS3 GRP4 LEVELS4  SUMWGT STDDEV) REST_ALL(IN=B KEEP=YEAR GRP3 LEVELS3 GRP4 LEVELS4 TOTEVT TOTEVT_SE);
BY YEAR GRP3 LEVELS3 GRP4 LEVELS4;
IF A;
TOTEVTX=TOTEVT/1000000;
TOTEVT_SEX=TOTEVT_SE/1000000;
SUMWGTX=SUMWGT/1000000;
STDDEVX=STDDEV/1000000;
DIFF_SUM=abs(round(SUM(TOTEVTX,-SUMWGTX), 0.1));
DIFF_SE=abs(round(SUM(TOTEVT_SEX, -STDDEVX),0.1));
RUN;

TITLE2 "# OF EVENTS (in millions) BY CROSS SECTIONAL 14 DEMOGRAPHIC/HEALTH/SOCIO-ECONOMIC STATUS, FOR 1996-2014";
PROC PRINT DATA=ALL(WHERE=(DIFF_SUM NE 0 OR DIFF_SE NE 0)) LABEL;
VAR YEAR GRP3 LEVELS3 GRP4 LEVELS4 TOTEVTx SUMWGTx DIFF_SUM TOTEVT_SEx STDDEVx DIFF_SE;
LABEL TOTEVTx="# of event R"
 SUMWGTx="# of event SAS"
  DIFF_SUM="# of event Diff" 
  TOTEVT_SEx="SE R"
  STDDEVx="SE SAS" 
  DIFF_SE="SE Diff"
  ;
FORMAT TOTEVTx SUMWGTx DIFF_SUM TOTEVT_SEx STDDEVx DIFF_SE COMMA20.1;
RUN;

%jobend;
