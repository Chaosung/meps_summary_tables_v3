%JOBSTART;
*********************************************************************
 * Task Number :    AH4.FF004                                                                                                                        
 * Program Name:    \\\programs.ahrq.local\programs\MEPS\AHRQ4_CY2\F_DSO\FF004ETH\Prog\SAS\TOTEVT_cross2.sas                                                                                                
 * Description :    Calculate cross section estimates - TOTEXP by demo*sop and compare them with V4 of R estimates for 1996-2014                                                                                                                                                                                                
 * Input Data:	   \\derived.ahrq.local\Derived\MEPS\AHRQ4_CY2\F_DSO\FF004ETH\Data\PERSDATA9614, EVTdata9614.sas7bdat  
 *                  \\derived.ahrq.local\derived\MEPS\AHRQ4_CY2\F_DSO\FF004ETH\R_Estimates\V4\EVT9614
 * Output Data :    no                                                      
 * Programmer  :    Bidong Liu                                                                                                                                      
 * Date        :    5/31/2017
*********************************************************************;
OPTIONS LS=200 PS=60 OBS=MAX ;
libname task "\\derived.ahrq.local\Derived\MEPS\AHRQ4_CY2\F_DSO\FF004ETH\Data";
libname V4R "\\derived.ahrq.local\derived\MEPS\AHRQ4_CY2\F_DSO\FF004ETH\R_Estimates\V4";

title1 "AH4.FF004";
PROC FORMAT;
VALUE AGEGRPS
1='UNDER 5'
2='5-17'
3='18-44'
4='45-64'
5='65+'
;
VALUE AGEGRPS_V2X
1='UNDER 18'
2='18-64'
3='65+'
;

VALUE MARRIED
.='. MISSING'
1='MARRIED'
2='WIDOWED'
3='DIVORCED'
4='SEPARATED'
5='NEVER MARRIED'
6='Inapplicable (age < 16)'
;

VALUE INSURANCE
1='ANY PRIVATE'
2='PUBLIC ONLY'
3='UNINSURED'
;

VALUE HEALTH
.=". MISSING" 
1='EXCELLENT' 
2='VERY GOOD' 
3='GOOD' 
4='FAIR' 
5='POOR'
;

VALUE MENTAL_HEALTH
.=". MISSING"
1='EXCELLENT'
2='VERY GOOD'
3='GOOD'
4='FAIR'
5='POOR'
;

VALUE EMPLOYED
.='. MISSING'
-1='Inapplicable (age < 16)'
1='EMPLOYED'
2='NOT EMPLOYED'
;

VALUE RACE
1='HISPANIC'
2='WHITE'
3='BLACK'
4='American Indian, Alaska Native,'
5='Asian, Hawaiian, or Pacific Isla'
9='WHITE AND OTHER'
;

VALUE EDUCATION
.='. MISSING'
1='LESS THAN HIGH SCHOOL'
2='HIGH SCHOOL'
3='SOME COLLEGE'
4='Inapplicable (age < 18)'
;

VALUE insurance_v2X
1='<65, Any private'
2='<65, Public only'
3='<65, UNINSURED'
4='65+, MEDICARE ONLY'
5='65+, Medicare and private'
6='65+, Medicare and other public'
7='65+, NO MEDICARE'
;

VALUE POVERTY
1='Negative or Poor'
2='Near-poor'
3='LOW INCOME'
4='MIDDLE INCOME'
5='HIGH INCOME'
;

VALUE SEX
1='Male'
2='Female';

VALUE REGION
1='NORTHEAST'
2='MIDWEST'
3='SOUTH'
4='WEST';

VALUE IND
1='Total';
RUN;



DATA RESULT;
   LENGTH  LEVELS2 $32;
levels2="XXXXXXXXXXXXXXXXX";
IF _N_ LT 0;
RUN;



%MACRO YY;
%DO YEAR=1996 %TO 2014;

TITLE3 "YEAR=&YEAR";

DATA FY;
  SET TASK.PERSDATA9614 (WHERE=(YEAR=&YEAR));
  TOTAL=1;

RUN;

ODS LISTING CLOSE;
PROC SURVEYMEANS DATA=FY SUM STD  missing;
VAR TOTEXP sop1exp sop2exp sop3exp sop4exp sop5exp;
CLUSTER VARPSU; 
STRATA VARSTR;
WEIGHT PERWTF;
DOMAIN TOTAL AGEGRPS AGEGRPS_V2 REGION MARRIED RACE SEX INSURANCE INSURANCE_V2 HEALTH MENTAL_HEALTH EDUCATION EMPLOYED POVERTY;
ODS OUTPUT domain=OUT;
RUN;
ODS LISTING;

DATA OUT1;
SET OUT;
YEAR=&YEAR;
RUN;

DATA RESULT;
SET RESULT OUT1;
RUN;

%END;
%MEND YY;
%YY



data RESULT1(WHERE=(GRP2 NE " ") KEEP=GRP: LEVELS: SUM STDDEV YEAR);
set RESULT;
  LENGTH grp2 grp1 $15  LEVELS2 levels1 $32;
  IF VARNAME="TOTEXP" THEN DO; GRP1="SOP"; LEVELS1="EXP";END;
  ELSE IF VARNAME="SOP1EXP" THEN DO; GRP1="SOP"; LEVELS1="SLF";END;
  ELSE IF VARNAME="SOP2EXP" THEN DO; GRP1="SOP"; LEVELS1="PTR";END;
  ELSE IF VARNAME="SOP3EXP" THEN DO; GRP1="SOP"; LEVELS1="MCR";END;
  ELSE IF VARNAME="SOP4EXP" THEN DO; GRP1="SOP"; LEVELS1="MCD";END;
  ELSE IF VARNAME="SOP5EXP" THEN DO; GRP1="SOP"; LEVELS1="OTZ";END;
  
  
  IF TOTAL=1 THEN DO; GRP2='IND'; LEVELS2='TOTAL'; END;
  ELSE IF AGEGRPS>0 THEN DO; GRP2='AGEGRPS'; LEVELS2=PUT(AGEGRPS, AGEGRPS.);END;
  ELSE IF AGEGRPS_V2>0 THEN DO; GRP2='AGEGRPS';LEVELS2=PUT(AGEGRPS_V2, AGEGRPS_V2X.);END;
  ELSE IF REGION>0 THEN DO; GRP2='REGION'; LEVELS2=PUT(REGION, REGION.);END;
  ELSE IF MARRIED>0 OR MARRIED=-1 THEN DO;GRP2='MARRIED'; LEVELS2=PUT(MARRIED, MARRIED.);END;
  ELSE IF RACE>0 THEN DO;GRP2='RACE' ; LEVELS2=PUT(RACE, RACE.);END;
  ELSE IF SEX>0 THEN DO; GRP2='SEX' ; LEVELS2=PUT(SEX, SEX.);END;
  ELSE IF INSURANCE>0 THEN DO; GRP2='INSURANCE' ; LEVELS2=PUT(INSURANCE, INSURANCE.);END;
  ELSE IF INSURANCE_V2>0 THEN DO; GRP2='INSURANCE' ; LEVELS2=PUT(INSURANCE_V2, INSURANCE_V2X.);END;
  ELSE IF HEALTH>0 THEN DO; GRP2='HEALTH' ; LEVELS2=PUT(HEALTH, HEALTH.);END;
  ELSE IF MENTAL_HEALTH>0 THEN DO; GRP2='MENTAL_HEALTH' ; LEVELS2=PUT(MENTAL_HEALTH, MENTAL_HEALTH.);END;
  ELSE IF EDUCATION>0 OR EDUCATION=-1 THEN DO; GRP2='EDUCATION' ; LEVELS2=PUT(EDUCATION, EDUCATION.);END;
  ELSE IF EMPLOYED>0 OR EMPLOYED=-1 THEN DO; GRP2='EMPLOYED' ; LEVELS2=PUT(EMPLOYED, EMPLOYED.);END;
  ELSE IF POVERTY>0 THEN DO; GRP2='POVERTY' ; LEVELS2=PUT(POVERTY, POVERTY.);END;

RUN;





PROC SORT DATA=V4R.FYC9614 OUT=REST(KEEP=GRP1 LEVELS1 GRP2 LEVELS2 TOTEXP TOTEXP_SE YEAR);
BY YEAR GRP1 LEVELS1 GRP2 LEVELS2;
RUN;

DATA REST2;
SET REST;
 LENGTH GRP3 GRP4 $15  LEVELS3 LEVELS4 $32;
GRP3=LOWCASE(COMPRESS(GRP1));
LEVELS3=LOWCASE(COMPRESS(LEVELS1));
GRP4=LOWCASE(COMPRESS(GRP2));
LEVELS4=LOWCASE(COMPRESS(LEVELS2));
RUN;

DATA REST3(DROP=GRP3 GRP4 LEVELS3 LEVELS4);
SET REST2;
GRP5=GRP3;
LEVELS5=LEVELS3;
GRP6=GRP4;
LEVELS6=LEVELS4;
RUN;


DATA REST4(DROP=GRP5 GRP6 LEVELS5 LEVELS6);
SET REST3;
GRP4=GRP5;
LEVELS4=LEVELS5;
GRP3=GRP6;
LEVELS3=LEVELS6;
RUN;

DATA REST_ALL;
SET REST2 REST4;
RUN;

PROC SORT DATA=REST_ALL nodupkey;
BY YEAR GRP3 LEVELS3 GRP4 LEVELS4;
RUN;


DATA RESULT2;
SET RESULT1;
 LENGTH GRP3 GRP4 $15  LEVELS3 LEVELS4 $32;
GRP3=LOWCASE(COMPRESS(GRP1));
LEVELS3=LOWCASE(COMPRESS(LEVELS1));
GRP4=LOWCASE(COMPRESS(GRP2));
LEVELS4=LOWCASE(COMPRESS(LEVELS2));
RUN;


PROC SORT DATA=RESULT2 nodupkey;
BY YEAR GRP3 LEVELS3 GRP4 LEVELS4;
RUN;

DATA ALL;
MERGE RESULT2(IN=A KEEP=YEAR GRP3 LEVELS3 GRP4 LEVELS4  SUM STDDEV) REST_ALL(IN=B KEEP=YEAR GRP3 LEVELS3 GRP4 LEVELS4 TOTEXP TOTEXP_SE);
BY YEAR GRP3 LEVELS3 GRP4 LEVELS4;
IF A;
TOTEXPX=TOTEXP/1000000000;
TOTEXP_SEX=TOTEXP_SE/1000000000;
SUMX=SUM/1000000000;
STDDEVX=STDDEV/1000000000;
DIFF_SUM=abs(round(SUM(TOTEXPX,-SUMX), 0.1));
DIFF_SE=abs(round(SUM(TOTEXP_SEX, -STDDEVX),0.1));
RUN;

TITLE2 "TOTEXP (in billions) BY CROSS SECTIONAL DEMO*SOP, FOR 1996-2014";
PROC PRINT DATA=ALL(WHERE=(DIFF_SUM NE 0 OR DIFF_SE NE 0)) LABEL;
VAR YEAR GRP3 LEVELS3 GRP4 LEVELS4 TOTEXPx SUMx DIFF_SUM TOTEXP_SEx STDDEVx DIFF_SE;
LABEL TOTEXPx="TOTEXP R"
 SUMx="TOTEXP SAS"
  DIFF_SUM="TOTEXP Diff" 
  TOTEXP_SEx="SE R"
  STDDEVx="SE SAS" 
  DIFF_SE="SE Diff"
  ;
FORMAT TOTEXPx SUMx DIFF_SUM TOTEXP_SEx STDDEVx DIFF_SE COMMA20.1;
RUN;